(ns radish.main
  (:require [clojure.string :as str]
            [clojure.zip :as zip]
            [clojure.java.shell :refer [sh]]
            [clojure.tools.cli :as cli]
            [hiccup.core :refer [html]]
            [hiccup.page :as page]
            [orgmode.core :as org]
            [orgmode.html :refer [hiccupify *user-src-fn*]])
  (:gen-class))

;; https://ravi.pckl.me/short/functional-xml-editing-using-zippers-in-clojure/
(defn tree-edit
  [zipper matcher editor]
  (loop [loc zipper]
    (if (zip/end? loc)
      (zip/root loc)
      (if-let [matcher-result (matcher loc)]
        (let [new-loc (zip/edit loc editor)]
          (if (not (= (zip/node new-loc) (zip/node loc)))
            (recur (zip/next new-loc))))
        (recur (zip/next loc))))))

(defn get-nodes
  [zipper matcher]
  (loop [loc zipper
         acc []]
    (if (zip/end? loc)
      acc
      (if (matcher loc)
        (recur (zip/next loc) (conj acc (zip/node loc)))
        (recur (zip/next loc) acc)))))

(defn match-result?
  [loc]
  (let [node (zip/node loc)
        s (-> node :content first)]
    (when s
      (str/starts-with?
       (str/upper-case s)
       "#+RESULT"))))

;; make it a requirement that the deps src be headlined as deps or deps.edn
(defn match-deps?
  [loc]
  (let [node (zip/node loc)
        {:keys [type text]} node]
    (and (= type :headline)
         (str/starts-with? text "deps"))))

(defn match-headlines?
  [loc]
  (let [node (zip/node loc)]
    (= (:type node) :headline)))
    
;; don't remove the entire node as the #+RESULT is within a paragraph
;; which means there may be required content following the results.
(defn remove-result
  [node]
  (let [new-content (drop 2 (:content node))]
    (assoc node :content (vec new-content))))

(defn remove-results
  [org]
  (let [org-zipper (org/zip org)]
    (tree-edit org-zipper match-result? remove-result)))

(defn get-headlines
  [org]
  (map :text (get-nodes (org/zip org) match-headlines?)))

(defn find-title
  [org-str]
  (let [lines (str/split-lines org-str)
        headlines (filter #(not (str/starts-with? % ";"))
                          (get-headlines (org/parse-str org-str)))
        f #(str/starts-with? (str/upper-case %) "#+TITLE")
        title (->> lines (filter f) first)]
    (if title
      (str/join " " (-> title (str/split #" ") rest))
      (first headlines))))

(defn safe-name
[title]
(-> title
    str/lower-case
    (str/replace #";" "-")
    (str/replace #" " "-")))

(defn find-author
[org-str]
(let [lines (str/split-lines org-str)
      f #(str/starts-with? (str/upper-case %) "#+AUTHOR")
      title (->> lines
                 (filter f)
                 first)]
  (when title
    (str/join " " (-> title (str/split #" ") rest)))))

(defn find-deps
  [org-str]
  (-> org-str
      org/parse-str
      org/zip
      (get-nodes match-deps?)
      (get-in [0 :content 0 :content])
      (->> (apply str))
      read-string
      (#(apply dissoc % (remove #{:deps} (keys %))))))

(defn src-fn
  [x]
  (let [class (str "src-" (first (:attribs x)))]
    [:div.code-container
     [:pre {:class class} (str/join "\n" (:content x))]]))

(defn org-content
  [org-str]
  (let [title (find-title org-str)
        author (find-author org-str)]
    (list
     [:header [:h1 title]]
     [:main (-> org-str
                org/parse-str
                remove-results
                hiccupify)]
     [:footer
      (when author
        [:p "Written by " [:span {:style {:font-style "italic"}} author]])
      [:p "Generated by "
       [:span {:style {:font-weight "bold"}}
        [:a {:href "https://github.com/adam-james-v/radish"} "radish"]]]])))

(defn org->site
  [org-str]
  (let [title (find-title org-str)
        author (find-author org-str)
        org-content (into [:body] (org-content org-str))]
    (page/html5
     [:head
      [:meta {:charset "utf-8"}]
      [:title title]
      (page/include-css
       "style.css"
       "codemirror.css"
       "nord.css")
      (page/include-js
       "codemirror.js"
       "clojure.js")
      (page/include-js
       "https://cdn.jsdelivr.net/gh/borkdude/scittle@0.0.2/js/scittle.js"
       "https://unpkg.com/react@17/umd/react.production.min.js"
       "https://unpkg.com/react-dom@17/umd/react-dom.production.min.js"
       "https://cdn.jsdelivr.net/gh/borkdude/scittle@0.0.2/js/scittle.reagent.js")
      [:script {:type "application/x-scittle"}
       (slurp (clojure.java.io/resource "code-runner.cljs"))]]
     org-content)))

(defn basic-build!
  [org-str]
  (let [name (safe-name (find-title org-str))
        index (binding [*user-src-fn* src-fn] (org->site org-str))]
    (sh "mkdir" "-p" name)
    (doseq [file ["style.css"
                  "codemirror.css"
                  "nord.css"
                  "codemirror.js"
                  "clojure.js"]]
      (spit (str name "/" file) (slurp (clojure.java.io/resource file))))
    (spit (str name "/index.html") index)
    ;; sh uses futures in different threads, so shut them down
    (shutdown-agents)))

#_(def a (slurp "/Users/adam/design/radish-logo/radish-logo.org"))
#_(def b (org/parse-str a))

(def cljs-deps
  '{org.clojure/clojurescript {:mvn/version "1.10.773"}
   reagent/reagent {:mvn/version "0.10.0"}
   borkdude/sci {:mvn/version "0.2.6"}
   com.bhauman/figwheel-main {:mvn/version "0.2.6"}})

(defn prepare-deps
  [org-str]
  (update (find-deps org-str) :deps merge cljs-deps))

(defn prepare-doc
  [org-str]
  (let [org-content (binding [*user-src-fn* src-fn] (org-content org-str))]
    '(defn doc [] [:<> org-content])))

(defn advanced-build-index
  [org-str]
  (let [title (find-title org-str)]
    (page/html5
     [:head
      [:meta {:charset "utf-8"}]
      [:title title]
      (page/include-css
       "style.css"
       "codemirror.css"
       "nord.css")
      (page/include-js
       "codemirror.js"
       "clojure.js"
       "prod-main.js")]
     [:div {:id "root"}])))

(defn advanced-build!
  [org-str]
  (let [name (safe-name (find-title org-str))
        index (advanced-build-index org-str)
        doc (prepare-doc org-str)]
    (sh "mkdir" "-p" name)
    (doseq [file ["style.css"
                  "codemirror.css"
                  "nord.css"
                  "codemirror.js"
                  "clojure.js"]]
      (spit (str name "/" file) (slurp (clojure.java.io/resource file))))
    (spit (str name "/index.html") index)
    (spit (str name "/main.cljs") doc)
    ;; sh uses futures in different threads, so shut them down
    #_(shutdown-agents)))

(def cli-options
  [["-i" "--infile FNAME" "The file to be compiled."
    :default nil]
   ["-h" "--help"]])

(defn -main
  [& args]
  (let [parsed (cli/parse-opts args cli-options)
        {:keys [:infile :help]} (:options parsed)
        [in _] (when infile (str/split infile #"\."))]
    (cond
      help
      (do (println "Usage:")
          (println (:summary parsed)))
          
      (nil? infile)
      (println "Please specify an input file")
      
      :else
      (let [org-str (slurp infile)
            outdir (safe-name (find-title org-str))
            msg (str "Compiling " infile " into directory " outdir ".")]
        (println msg)
        (basic-build! org-str)
        (println "Success! Have a nice day :)")))))
